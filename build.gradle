
buildscript {
    repositories {
        maven { url 'https://extranet.aoe.com/artifactory/remote-repos' }
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-publishing-plugin:4.4.4'
        classpath 'com.netflix.nebula:nebula-release-plugin:3.1.0'
    }
}

ext {
    versions = [:]
}

versions.jenkins = '1.565'
versions.jobDsl = '1.40'
versions.groovy = '1.8.9'
versions.spock = '0.7-groovy-1.8'

apply plugin: 'nebula.nebula-release'

allprojects {
    group = 'com.aoe.gradle'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'nebula.maven-base-publish'

    repositories {
        maven { url 'https://extranet.aoe.com/artifactory/remote-repos' }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    publishing {
        repositories {
            maven {
                if (project.version.toString().endsWith('-SNAPSHOT')) {
                    url 'https://extranet.aoe.com/artifactory/libs-snapshot-local'
                } else {
                    url 'https://extranet.aoe.com/artifactory/libs-release-local'
                }
                credentials {
                    username = project.hasProperty('aoeRepoUsername') ? aoeRepoUsername : ''
                    password = project.hasProperty('aoeRepoPassword') ? aoeRepoPassword : ''
                }
            }
        }
    }
}

tasks.release.finalizedBy subprojects.collect { it.tasks.publish }
